title: Young Plan campaigning
new-page: true
subtitle: How should we campaign in favor of the Plan?
tags: event
view-if: year = 1929 and month >= 9
max-visits: 1

= Young Plan Referendum

While the Young Plan seems to be a positive development for the country, the Right are gathering signatures to initiate a referendum this December. While the referendum itself is likely to fail, their propaganda is striking a chord with many, especially amid the ongoing depression. It comes down to us to campaign against the rhetoric of those that seek to destroy our republic. [? if spd_in_government: We are able to use government resources for this campaign, lowering the cost for our party. ?]

- @pro_plan: Campaign in favor of the Young Plan and against the referendum.
- @no_plan: Do not campaign on the Young Plan, one way or another.

@pro_plan
subtitle: [? if spd_in_government: -2 resources ?][? if not spd_in_government: -1 resources ?]
choose-if: (resources >= 2 if spd_in_government = 1) or (resources >= 1 if spd_in_government = 0)
unavailable-subtitle: Not enough resources.
on-arrival: resources -= 2 if spd_in_government; resources -= 1 if not spd_in_government;

How should we campaign in favor of the Plan?

- @internationalism: As part of a broader campaign in favor of internationalism and pacifism.
- @rational: The Young Plan is simply the most rational choice for Germany.

@internationalism
on-arrival: pro_republic -= 3 if pacifism <= 3; old_middle_spd += 2 if radio >= 2; new_middle_spd += 2 if radio >= 2; nationalism -= 5*(1-dissent) if pacifism >= 2 else nationalism += 3; pacifism += 1; workers_spd += 5*(1-dissent) if pacifism >= 3 and nationalism <= 50; workers_spd += 2*(1-dissent) if radio >= 2; workers_spd += 1; workers_nsdap += 4 if pacifism <= 3; rural_nsdap += 5; catholics_spd -= 4 if pacifism <= 3; rural_dvp -= 4

We rail hard against nationalism and in favor of pacifism and belonging to the international community of nations. [? if radio >= 2: Our radio network has helped us spread our message.&nbsp;?][? if pacifism >= 3 and nationalism <= 50 : We have rallied the workers to the cause of internationalism. ?]Although the bourgeois are not too pleased for us having monopolized the campaign for the Young Plan.


@rational
subtitle: The bourgeois will be more inclined to work with us on this.
on-arrival: pro_republic -= 3; pro_republic += 2 if z_relation >= 50 and dvp_relation >= 35; nationalism += 5; nationalism -= 2 if z_relation >= 50 and dvp_relation >= 35; old_middle_spd += 2 if radio >= 2; new_middle_spd if radio >= 2; old_middle_spd += 2; new_middle_spd += 2; workers_nsdap += 4; rural_nsdap += 5; rural_nsdap -= 3 if z_relation >= 50 and dvp_relation >= 35; catholics_spd -= 4; workers_spd -= 3; workers_spd += 1 if radio >= 2; rural_dvp -= 4; rural_dvp += 2 if z_relation >= 50 and dvp_relation >= 35; z_relation += 5; dvp_relation += 5


We argue that the Young Plan is simply the most rational choice for Germany. Although it doesn't advance any broader political objectives, most people can get behind economic stability and recovery. [? if z_relation >= 50 and dvp_relation >= 35: The bourgeois parties have rallied with us to campaign against the referendum and for the Young Plan.&nbsp;?][? if radio >= 2: Our radio network has helped us spread our message.&nbsp;?]


@no_plan
on-arrival: nationalism += 5; workers_spd -= 3; old_middle_spd -= 3; new_middle_spd -= 3; pacifism -= 1; workers_nsdap += 5; rural_nsdap += 5; rural_dnvp -= 5; catholics_spd -= 5; rural_dvp -= 5; pro_republic -= 5

The referendum easily fails with or without our presence in the campaign, with only around 15% of voters supporting it. However, nationalist sentiment is inflammed, and the right-wing parties are using our silence around the Plan as a tacit admission of its faults. Our silence has only emboldened the Right, especially the <span style="color: #7A3C00;">**NSDAP**</span>.
